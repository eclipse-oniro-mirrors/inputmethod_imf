# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/inputmethod/imf/inputmethod.gni")
import("//build/ohos.gni")

config("ability_config") {
  visibility = [ ":*" ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  defines = [
    "APP_LOG_TAG = \"Ability\"",
    "LOG_DOMAIN = 0xD002200",
  ]
}

config("ability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [ "${inputmethod_path}/frameworks/kits/extension/include" ]
}

ohos_shared_library("inputmethod_extension") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  cflags_cc = [
    "-fdata-sections",
    "-ffunction-sections",
    "-Os",
  ]
  include_dirs = [
    "${inputmethod_path}/frameworks/js/napi/common",
    "${inputmethod_path}/frameworks/js/napi/inputmethodclient",
    "${inputmethod_path}/frameworks/kits/extension/include",
    "${inputmethod_path}/frameworks/native/inputmethod_controller/include",
    "${inputmethod_path}/interfaces/inner_api/inputmethod_controller/include",
    "${inputmethod_path}/common/include",
  ]

  sources = [
    "${inputmethod_path}/frameworks/js/napi/inputmethodclient/js_utils.cpp",
    "${inputmethod_path}/frameworks/kits/extension/src/inputmethod_extension.cpp",
    "${inputmethod_path}/frameworks/kits/extension/src/inputmethod_extension_context.cpp",
    "${inputmethod_path}/frameworks/kits/extension/src/js_inputmethod_extension.cpp",
    "${inputmethod_path}/frameworks/kits/extension/src/js_inputmethod_extension_context.cpp",
  ]
  configs = [ ":ability_config" ]
  public_configs = [ ":ability_public_config" ]

  deps = [
    "${inputmethod_path}/common:inputmethod_common",
    "${inputmethod_path}/frameworks/js/napi/common:inputmethod_js_common",
    "${inputmethod_path}/interfaces/inner_api/inputmethod_ability:inputmethod_ability",
  ]

  external_deps = [
    "ability_base:configuration",
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",
    "ability_runtime:app_context",
    "ability_runtime:napi_common",
    "ability_runtime:runtime",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "i18n:intl_util",
    "input:libmmi-client",
    "ipc:ipc_napi",
    "ipc:ipc_single",
    "napi:ace_napi",
    "samgr:samgr_proxy",
    "window_manager:libdm",
  ]

  subsystem_name = "inputmethod"
  part_name = "imf"
}

ohos_shared_library("inputmethod_extension_module") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  include_dirs = [ "${inputmethod_path}/common/include" ]

  sources = [ "${inputmethod_path}/frameworks/kits/extension/src/inputmethod_extension_module_loader.cpp" ]

  configs = [ ":ability_config" ]
  public_configs = [ ":ability_public_config" ]

  deps = [ ":inputmethod_extension" ]

  external_deps = [
    "ability_base:base",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:abilitykit_native",
    "ability_runtime:runtime",
    "c_utils:utils",
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  relative_install_dir = "extensionability/"
  subsystem_name = "inputmethod"
  part_name = "imf"
}
